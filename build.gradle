import java.nio.file.Files
import java.nio.file.StandardCopyOption
import com.github.jengelman.gradle.plugins.shadow.transformers.Log4j2PluginsCacheFileTransformer

plugins {
    id 'java'
    id 'idea'
    id 'application'

    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'com.github.johnrengelman.shadow' version '4.0.2'
}

version '1'

sourceSets {
    spike
}

dependencies {
    compile 'com.google.apis:google-api-services-youtube:v3-rev206-1.25.0'
    
    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.12.0'

    compile 'com.google.guava:guava:27.0-jre'

    compile 'org.apache.commons:commons-lang3:3.8.1'
    compile 'org.apache.commons:commons-collections4:4.2'
    compile 'org.apache.commons:commons-math3:3.6.1'
    compile 'org.apache.commons:commons-text:1.6'
    compile 'commons-io:commons-io:2.6'

    compile 'org.apache.logging.log4j:log4j-core:2.11.1'
    compile 'org.apache.logging.log4j:log4j-api:2.11.1'

    testCompile 'org.junit.jupiter:junit-jupiter-api:5.3.1'

    testCompile 'org.assertj:assertj-core:3.11.1'
    testCompile 'org.assertj:assertj-guava:3.2.0'

    testCompile 'org.mockito:mockito-all:2.0.2-beta'
}

configurations {
    spikeCompile {
        extendsFrom compile
    }
}

mainClassName = 'eu.goodlike.Main'
project.buildDir = '.build'
def projectPath = project.rootDir.toPath()
def jarPath = projectPath.resolve('artifacts')

shadowJar {
    transform(Log4j2PluginsCacheFileTransformer.class)
    mergeServiceFiles()
    
    baseName = rootProject.name
    classifier = null
    version = project.version

    destinationDir = jarPath.toFile()
    includeEmptyDirs = false
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
        resources {
            srcDirs = ['config-credentials', 'config-public']
        }
    }
    spike {
        java {
            srcDirs = ['spike']
        }
        resources {
            srcDirs = ['config-public']
        }
    }
    test {
        java {
            srcDirs = ['test']
        }
        resources {
            srcDirs = ['test-config']
        }
    }
}

idea {
    module {
        excludeDirs += file('.build')
        excludeDirs += file('.gradle')
        excludeDirs += file('.idea')
        excludeDirs += file('.logs')
        excludeDirs += file('.wrapper')

        excludeDirs += file('artifacts')

        inheritOutputDirs = true

        iml {
            generateTo = file('.idea')
        }
    }

    project {
        outputFile = new File('.idea', outputFile.getName())
    }
}

tasks.idea {
    doLast {
        def iwsFile = projectPath.resolve(rootProject.name + '.iws')
        if (Files.exists(iwsFile)) {
            Files.move(iwsFile, iwsFile.getParent().resolve('.idea').resolve(iwsFile.getFileName()),
                    StandardCopyOption.REPLACE_EXISTING)
        }
    }
}

test {
    useJUnitPlatform()
}

dependencyUpdates {
    outputDir = '/.build/dependencyUpdates'
}

repositories {
    jcenter()
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.10.2'
    jarFile = projectPath.resolve('.wrapper').resolve('gradle-wrapper.jar').toFile()
}

group 'eu.goodlike'

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
    options.encoding = 'UTF-8'
}
